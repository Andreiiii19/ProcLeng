//*****************************************************************
// File:   cl.jj
// Author: Procesadores de Lenguajes-University of Zaragoza
// Date:   julio 2023
// Coms:   compilar mediante "ant"
//*****************************************************************

options {
	IGNORE_CASE = true;
	COMMON_TOKEN_ACTION = true;
}

PARSER_BEGIN(alike)

package traductor;

//...

import lib.symbolTable.*;
import lib.attributes.*;
import lib.symbolTable.exceptions.*;
import java.util.ArrayList;
import lib.errores.*;
import lib.tools.SemanticFunctions;

public class alike { 


	static SymbolTable st;
	static SemanticFunctions sf = new SemanticFunctions();

	private static void initSymbolTable() {
		boolean b;
		String[] palsRes = {
	    	"array","boolean","character","integer","procedure","if","then","elsif","else","end",
			"while","loop","begin","ref","of","is","not","put_line","put","return","and","or",
			"mod","skip_line","function","null","get","true","false","int2char","char2int"
		};

		st = new SymbolTable();

		st.insertReservedWords(palsRes);
	}



   public static void main(String[] args) {
	   alike parser = null;
	   initSymbolTable();
	   
	   try {
		   if(args.length == 0) {
			   parser = new alike(System.in);
		   }
		   else {
			   parser = new alike(new java.io.FileInputStream(args[0]));
		   }
		   //Programa es el símbolo inicial de la gramática
		   parser.Programa();
		   System.out.println(st.toString());
		   //...
		   System.out.println("***** Análisis terminado con éxito *****");
	   }
	   catch (java.io.FileNotFoundException e) {
		   System.err.println ("Fichero " + args[0] + " no encontrado.");
	   }
	   catch (TokenMgrError e) {
		   System.err.println("LEX_ERROR: " + e.getMessage());
	   }
	   catch (ParseException e) {
            System.err.println("PARSER_ERROR: " + e.getMessage());
        }
		catch (ErrorSemantico e) {
			System.err.println("SEMANTIC_ERROR: " + e.getMessage());
			System.err.println("***** Análisis parado por un error *****");
		}
	   //...
   }
}
PARSER_END(alike)

TOKEN_MGR_DECLS : {
	static void CommonTokenAction(Token token) {
		System.err.println("(" + token.beginLine + ", "
					+ token.beginColumn + "): "+ alikeConstants.tokenImage[token.kind] + " " + token.image);
	}

}

SPECIAL_TOKEN : {
	< tCOMMENT: (<COMMENT>) >
}

TOKEN : {
	< #LETTER: (["a"-"z","A"-"Z"]) >
|	< #DIGIT: ["0"-"9"] >
|	< #CHAR: (~["\\"]) >
|	< #STRING: (~["\\","\""]|("\"\""))+ >
|	< #COMMENT: ("--")(~["\n"])*("\n") >
}

TOKEN : {
	< tBOOL: "boolean" >
|	< tCHAR: "character" >
|	< tINT: "integer" >
|	< tARRAY: "array" >
|	< tPROC: "procedure" >
|	< tIF:	"if" >
|	< tTHEN: "then" >
|	< tELSIF: "elsif" >
|	< tELSE: "else" >
|	< tENDIF: "end"(" ")+"if"> 
| 	< tEND: "end" >
|	< tWHILE: "while" >
|	< tLOOP: "loop" >
|	< tENDLOOP: "end loop" >
|	< tBEGIN: "begin" >
|	< tREF: "ref" >
|	< tOF: "of" >
|	< tIS: "is" >
|	< tNOT: "not" >
| 	< tPC: ";" >
|	< tPLINE: "put_line" >
|	< tPUT: "put" >
|	< tRETURN: "return" >
|	< tAP: "(" >
|	< tCP: ")" >
|	< tASIG: ":=" >
|	< tGE: ">=" >
|	< tLE: "<=" >
|	< tNEQ: "/=" >
|	< tEQ: "=" >
|	< tGT: ">" >
|	< tLS: "<" >
|	< tDP: ":" >
|	< tAND: "and" >
|	< tOR: "or" >
|	< tADD: "+" >
|	< tSUB: "-" >
|	< tDIV: "/" >
|	< tMUL: "*" >
|	< tCOMA: "," >
|	< tMOD: "mod" >
|	< tSKIP: "skip_line" >
|	< tFUNC : "function" >
|	< tNULL : "null" >
|	< tGET : "get" >
|	< tTRUE : "true" >
|	< tFALSE : "false" >
|	< tINT2CHAR : "int2char" >
|	< tCHAR2INT : "char2int" >
|	< tCONST_CHAR : ("'")(< CHAR >)("'") >
|	< tCONST_INT : (< DIGIT >)+ >
|	< tCONST_STRING : ("\"")(< STRING >)*("\"") >
| 	< tID: (< LETTER >| "_")+(< DIGIT >| "_" | < LETTER >)* > 
}

SKIP : 
{
	" "
|	"\t"
|	"\n"
|	"\r" 
}





//------------ Símbolo inicial de la gramática. Para análisis léxico no hace falta más
void Programa():
{
	Token name;
	Attributes at = new Attributes();
} 
{
	<tPROC>
	name = <tID>
	(<tAP> parametros_formales(at) <tCP>)?
	<tIS>
	( declaracion_variables() )?
	( (declaracion_procs_funcs())+ )?
	<tBEGIN>
	instrucciones()
	<tEND>
	<tPC>
}

void declaracion_procedimiento():
{
	Token name;
	Attributes att = new Attributes();
}
{
	name = cabecera_procedimiento(att)
	( declaracion_variables() )?
	( declaracion_procs_funcs() )?
	<tBEGIN>
	instrucciones()
	<tEND>
	<tPC> 
	{
		//System.err.println("Bloque" + name.image + "terminado \n" + st.toString());
		st.removeBlock();
	}
}

void declaracion_funcion():
{
	Attributes atType = new Attributes();
	Attributes atIsArray = new Attributes();
	Attributes atts = new Attributes();
	Token name;
}
{
	name = cabecera_funcion(atType,atIsArray,atts)
	( declaracion_variables() )?
	( declaracion_procs_funcs() )?
	<tBEGIN>
	{
		
	}
	instrucciones()
	<tEND>
	<tPC>
	{
		//System.err.println("Bloque " + name.image + " terminado \n"+ st.toString());
		st.removeBlock();
		if(atType.type == Symbol.Types.ARRAY)
		{
			throw new ErrorSemantico("No se permiten funciones con retorno de vectores");
		}
	}
}

void declaracion_procs_funcs():
{}
{
	declaracion_procedimiento()
	| declaracion_funcion()
}

void instrucciones():
{}
{
	(instruccion()(";"))+
}


void declaracion_variables():
{

}
{
	( declaracion_var() ";")+
}

ArrayList<Symbol> declaracion_var():
{
	Attributes atType = new Attributes();
	Attributes atIsArray = new Attributes();
	ArrayList<Token> tokens;
	ArrayList<Symbol> symbols = new ArrayList<Symbol>();
}
{
		tokens = lista_ids()
		<tDP>
		(	
		<tREF>
			{
				atType.parClass = Symbol.ParameterClass.REF;
			}
		)?
		tipo_variable(atType,atIsArray)
		{	
			if(atType.parClass==Symbol.ParameterClass.NONE)atType.parClass=Symbol.ParameterClass.VAL;
			for(Token t : tokens)
			{
				Symbol S = null;

				
				//System.out.println(atType.type + " " + atIsArray.type);
				if(atType.type == Symbol.Types.BOOL) S = new SymbolBool(t.image, atType.parClass);
				if(atType.type == Symbol.Types.CHAR) S = new SymbolChar(t.image, atType.parClass);
				if(atType.type == Symbol.Types.INT) S = new SymbolInt(t.image, atType.parClass);
				if(atType.type == Symbol.Types.ARRAY) 
				{
					
					S = new SymbolArray(t.image, atType.inicio,atType.fin,atIsArray.type,atType.parClass);
				}

				symbols.add(S);
				
				try {
					st.insertSymbol(S);
				} catch (AlreadyDefinedSymbolException e)
				{
					System.err.println("Error semantico. Simbolo ya existente");
				}
			}

			return symbols;
		}
}

ArrayList<Token> lista_ids():
{
	ArrayList<Token> tokens = new ArrayList<Token>();
	Token t;
}
{
	(
		t=<tID>
			{
				if(!st.isReservedWord(t.image))
				{
					tokens.add(t);
				}
				else
				{
					throw new ErrorSemantico("ID no valido. Es una palabra reservada");
				}

			}
	)
	(
		<tCOMA>
		t=<tID>
			{
				if(!st.isReservedWord(t.image)) tokens.add(t);
				else throw new ErrorSemantico("ID no valido. Es una palabra reservada");
			}
	)*
		{
			System.err.println("Lista de ids ");
			return tokens;
		}
}

void lista_ids_o_string_o_inv(Attributes att):
{
	Attributes at = new Attributes();
}
{
	expresion(at)
	{
		att.atts.add(at.clone());
		at = new Attributes();
	}
	(<tCOMA> expresion(at) 
	{
		att.atts.add(at.clone());
		at = new Attributes();
	})*
	{
		att.type = att.atts.get(0).type;
		
	}
}


Token cabecera_procedimiento(Attributes att):
{
	Token name;
}
{
	<tPROC>
	name = <tID>
	{
		try {
			Symbol S = new SymbolProcedure(name.image,att.parList);
			st.insertSymbol(S);
			st.insertBlock();
		}catch (AlreadyDefinedSymbolException ex)
		{
			System.err.println("Error semantico. Simbolo ya existente");
		}
	}
	(<tAP> 
	parametros_formales(att)
	<tCP>)?
	<tIS>
	{
		return name;
	}
}

Token cabecera_funcion(Attributes atType,Attributes atIsArray,Attributes atts):
{
	Token name;
	SymbolFunction S = null;
}
{
	<tFUNC>
	name = <tID>
	{
		try{ 
			S = new SymbolFunction(name.image,atts.parList,atType.type);
			st.insertSymbol(S);
			st.insertBlock();
		} catch (AlreadyDefinedSymbolException ex)
		{
			System.err.println("Error semantico. Simbolo ya existente");
		}
		
	}
	(<tAP> parametros_formales(atts) <tCP>)?
	<tRETURN>
	tipo_variable(atType,atIsArray)
	<tIS>
	{
		S.returnType = atType.type;
		return name;
	}
}

void parametros_formales(Attributes att): 
{
	ArrayList<Symbol> symbols;
}
{
	symbols = declaracion_var()
	{
		// try {
			for(Symbol S : symbols)
			{
				att.parList.add(S);
		// 		st.insertSymbol(S);
			}
		// } catch (AlreadyDefinedSymbolException ex)
		// {
		// 	System.err.println("Error semantico. Simbolo ya existente");
		// }
	}
	(<tPC> 
	symbols = declaracion_var()
	{
		for(Symbol S : symbols)
		{
			att.parList.add(S);
			// st.insertSymbol(S);
		}
	}
	)*
}


void instruccion() :
{
	Attributes att = new Attributes();	
}
{
		inst_leer(att)
	|   inst_saltar_linea(att)
	|	inst_escribir(att)
	|   inst_escribir_linea(att)
	|   inst_invocacion_o_asignacion(att)
	|   inst_if(att)
	|   inst_while(att)
	|   inst_return(att)
	|   inst_null(att)
}

void inst_leer(Attributes att):
{}{<tGET> <tAP> lista_ids() <tCP>{System.err.println("Expresion: " + att);}}

void inst_saltar_linea(Attributes att):
{}{<tSKIP>{System.err.println("Expresion: " + att);}}

void inst_escribir(Attributes att):
{}{<tPUT> <tAP> lista_ids_o_string_o_inv(att)  <tCP>{System.err.println("Expresion: " + att);}}

void inst_escribir_linea(Attributes att):
{}{<tPLINE> ((<tAP> lista_ids_o_string_o_inv(att) <tCP>))?{System.err.println("Expresion: " + att);}}

void inst_invocacion_o_asignacion(Attributes att):
{
	Attributes at = new Attributes();
	Attributes at2 = new Attributes();
	Token t;
}
{
	t = <tID>
	{
		try {
			Symbol S = st.getSymbol(t.image);
			sf.cambiarTipos(at2, S);
			sf.asignar_valores(att,at2,t.image,S.type, S.parClass, st.level);
		} catch (SymbolNotFoundException ex)
		{
			throw new ErrorSemantico(t.image + " no existe.");
		}
	} 
	(<tAP> (function_call(at2)) 
	{
		sf.checkTypeFunctionCall(at2);
	} 
	<tCP>)? 	
	(<tASIG> expresion(at) 
	{
		sf.add_to_atts(att,at);
		sf.heredar_valores(att,at);
		sf.checkTypesAsignacion(at,at2);
		at = new Attributes();
		at2 = new Attributes();
	}
	)?
	{
		System.err.println("Expresion/es de inv o asignacion: " + att);
	}
}


// void inv_funcion(Attributes att):
// {
// 	Attributes at = new Attributes();
// }
// {
// 	(expresion(at) 
// 	{
// 		att.atts.add(at.clone());
// 	}
// 	(<tCOMA> expresion(at)
// 	{
// 		att.atts.add(at.clone());
// 	}
// 	)*)
// 	{System.err.println("Expresion/es de inv a funcion: " + att);}
// }

void inst_if(Attributes att):
{
	Attributes at = new Attributes();
}
{
	<tIF> expresion(at)
	{
		System.err.println("Expresion if tipo: " + at.type);
		if(at.type==Symbol.Types.ARRAY)
		{
			Attributes aux = at.atts.get(0);
			for(int i=0; i<6 && aux.atts.size()>0; i++)
			{
				aux = aux.atts.get(0);	
				System.err.println(aux.type);
			}
			if(aux.type!=Symbol.Types.INT)
			{
				throw new ErrorSemantico("No puedes comparar un array entero con un booleano");
			}
			if(at.extraType!=Symbol.Types.BOOL)
			{
				throw new ErrorSemantico("El array tienen que devolver un booleano: "+at.type);
			}
		} 
		else if(at.type== Symbol.Types.FUNCTION)
		{
			if(at.extraType!=Symbol.Types.BOOL)
			{
				throw new ErrorSemantico("La funcion tienen que devolver un booleano: "+at.type);
			}
		}
		else if(at.type!=Symbol.Types.BOOL)
		{
			throw new ErrorSemantico("Solo se admiten expresiones booleanas: "+at.type);
		}
		sf.add_to_atts(att,at);
		sf.heredar_valores(att,at);
		at = new Attributes();
	}
	<tTHEN> instrucciones() ((<tELSIF> expresion(at)
	{
		if(at.type==Symbol.Types.ARRAY)
		{
			if(at.extraType!=Symbol.Types.BOOL)
			{
				throw new ErrorSemantico("El array tienen que devolver un booleano: "+at.type);
			}
		} 
		else if(at.type== Symbol.Types.FUNCTION)
		{
			if(at.extraType!=Symbol.Types.BOOL)
			{
				throw new ErrorSemantico("La funcion tienen que devolver un booleano: "+at.type);
			}
		}
		else if(at.type!=Symbol.Types.BOOL)
		{
			throw new ErrorSemantico("Solo se admiten expresiones booleanas: "+at.type);
		}
		sf.add_to_atts(att,at);
		sf.heredar_valores(att,at);
		at = new Attributes();

		// if(at.type!=Symbol.Types.BOOL)
		// {
		// 	throw new ErrorSemantico("Solo se admiten expresiones booleanas: "+at.type);
		// }
	}
	<tTHEN> instrucciones())* (<tELSE> instrucciones())?) <tENDIF>
	{
		
		System.err.println("Expresion/es de ifs condiciones: " + att);
	}
} 

void inst_while(Attributes att):
{
	Attributes at = new Attributes();
}
{
	<tWHILE> expresion(at)
	{
		if(at.type==Symbol.Types.ARRAY || at.type==Symbol.Types.FUNCTION)
		{
			if(at.extraType!=Symbol.Types.BOOL)
			{
				throw new ErrorSemantico("Solo se admiten expresiones booleanas: "+at.type);
			}
		}
		else if(at.type!=Symbol.Types.BOOL)
		{
			throw new ErrorSemantico("Solo se admiten expresiones booleanas: "+at.type);
		}
		sf.add_to_atts(att,at);
		sf.heredar_valores(att,at);
	}
	<tLOOP> instrucciones() <tENDLOOP>
	{
		System.err.println("Expresion while condicion: " + att);
	}
}

void inst_return(Attributes att):{
	Attributes at = new Attributes();
}
{
	<tRETURN> expresion(at)
	{
		att.atts.add(at.clone());
		at = new Attributes();
	}
}

void inst_null(Attributes att):{}{<tNULL>{System.err.println("Expresion null");}	}


void expresion(Attributes att):
{
	Boolean esBool = false; 
	Attributes at = new Attributes();
}
{
    relacion(at)
	{
		sf.add_to_atts(att,at);
		sf.heredar_valores(att,at);
		at = new Attributes();
	}
	( ((<tAND> | <tOR>) 
	{
		att.type = Symbol.Types.BOOL;
		esBool = true;
	}
	relacion(at) 
	{
		sf.add_to_atts(att,at);
		sf.heredar_valores(att,at);
		at = new Attributes();
	}
	)+ )?
	{
		// ( 2+3+4) + 5 + (2+3)
		// Expresion
		//  (2+3+4)
		//   5
		//  (2+3)
		if(esBool)
		{
			 for(Attributes a : att.atts)
			 {
				System.err.println(a.type);
				if(a.type == Symbol.Types.FUNCTION || a.type == Symbol.Types.ARRAY){
					if(a.extraType!=Symbol.Types.BOOL)
					{
						throw new ErrorSemantico("Error de tipos");
					}
					if(a.type == Symbol.Types.ARRAY){
						Attributes aux = a.atts.get(0);
						for(int i=0; i<5 && aux.atts.size()>0; i++)
						{
							aux = aux.atts.get(0);	
							System.err.println(aux.type);
						}
						if(aux.type!=Symbol.Types.INT)
						{
							throw new ErrorSemantico("No puedes comparar un array entero con un booleano");
						}
					}
				} else{
					if(a.type!=Symbol.Types.BOOL)
					{
						throw new ErrorSemantico("Error de tipos");
					}
				}
			 }
			 //System.out.println();
		}
		
	}
}

void relacion(Attributes att):
{
	//Attributes atTypes = new Attributes();
	Boolean tieneOpInts = null;
	Attributes at = new Attributes();
}
{
    expresion_simple(at)
	{
		sf.add_to_atts(att,at);
		sf.heredar_valores(att,at);
		at = new Attributes();
	}
 	( tieneOpInts = operador_relacional()
	expresion_simple(at)
	{
		sf.add_to_atts(att,at);
		sf.heredar_valores(att,at);
		
	} 
	)? 
	{
		if(tieneOpInts!=null)
		{
			att.type = Symbol.Types.BOOL;
			if(tieneOpInts)
			{
				for(Attributes a : att.atts)
			 	{
			 		if(a.type!=Symbol.Types.INT)
			 		{
			 			throw new ErrorSemantico("Error de tipos. Se esperaban integers en relacion: "+
						att);
			 		}
			 	}
			} else {
				if(at.type != Symbol.Types.UNDEFINED) 
				{
					Attributes a2 = att.atts.get(att.atts.size()-2);
					if(a2.type == Symbol.Types.FUNCTION || a2.type == Symbol.Types.ARRAY)
					{
						if(a2.extraType!=at.type)
						{
							throw new ErrorSemantico("Error de tipos. Se esperaban el mismo tipo en relacion: " + a2.extraType + " y "+ at.type);
						}
					} else if(at.type == Symbol.Types.FUNCTION || at.type == Symbol.Types.ARRAY)
					{
						if(a2.type!=at.extraType)
						{
							throw new ErrorSemantico("Error de tipos. Se esperaban el mismo tipo en relacion: " + a2.type + " y "+ at.extraType);
						}
					}
					else if(a2.type!=at.type)
					{
						throw new ErrorSemantico("Error de tipos. Se esperaban el mismo tipo en relacion: " + a2.type + " y "+ at.type);
					}
				}
			}
		}
	}
}

Boolean operador_relacional():
{
	
}
{    
	<tEQ>    {return false;}// Char,bool,int
	| <tLS>  {return true;}// Int
	| <tGT>  {return true;}// Int
	| <tLE>  {return true;}// Int
	| <tGE>  {return true;}// Int
	| <tNEQ> {return false;}// Char,bool,int
}

void expresion_simple(Attributes att):
{
	boolean esInt=false;
	Attributes at = new Attributes();
}
{
	(( <tADD> |	<tSUB> )
	{
		esInt=true;att.type=Symbol.Types.INT;
	})? 
    termino(at)
	{
		sf.add_to_atts(att,at);
		sf.heredar_valores(att,at); 
		at = new Attributes();
	}
	( ( <tADD> | <tSUB> ) 
	termino(at) 
	{	
		sf.add_to_atts(att,at);
		sf.heredar_valores(att,at);
		at = new Attributes();
		att.type = Symbol.Types.INT;
		for(Attributes a : att.atts)
		{
			if(a.type!=Symbol.Types.INT)
			{
				throw new ErrorSemantico("Error de tipos. Se esperaban integers en expresion simple: " + a.type);
			}
		}
	})*
}

void termino(Attributes att):
{
	Attributes at = new Attributes();
}
{
    factor(at) 
	{
		sf.add_to_atts(att,at);
		sf.heredar_valores(att,at);
		at = new Attributes();
	}
	( operador_multiplicativo() factor(at)
	{
		sf.add_to_atts(att,at);
		sf.heredar_valores(att,at);
		att.type = Symbol.Types.INT;
		at = new Attributes();
		
		for(Attributes a : att.atts)
		{
			if(a.type!=Symbol.Types.INT)
			{
				throw new ErrorSemantico("Error de tipos. Se esperaban integers en termino: " + att.atts.get(0).type);
			}
		}
	}
	
	)*
}

void operador_multiplicativo():
{}
{
    <tMUL> | <tMOD> | <tDIV>
}

void factor(Attributes att):
{
	Attributes at = new Attributes();
}
{
    primario(at) 
	{
		sf.add_to_atts(att,at);
		sf.heredar_valores(att,at);
		at = new Attributes();
		
	}

|   <tNOT>  primario(at)
	{
		att.type = Symbol.Types.BOOL;
		sf.add_to_atts(att,at);
		sf.heredar_valores(att,at);
		if(at.type == Symbol.Types.ARRAY)
		{
			if(at.extraType!=Symbol.Types.BOOL)
			{
				throw new ErrorSemantico("El array tienen que devolver un booleano: "+at.type);
			}
		} 
		else if(at.type == Symbol.Types.FUNCTION)
		{
			if(at.extraType!=Symbol.Types.BOOL)
			{
				throw new ErrorSemantico("La funcion tienen que devolver un booleano: "+at.type);
			}
		}
		else if(at.type!=Symbol.Types.BOOL)
		{
			throw new ErrorSemantico("Solo se admiten expresiones booleanas: "+at.type);
		}
		at = new Attributes();
	}
}

void primario(Attributes att) :
{ 
	Attributes at = new Attributes();

	Token t;
	Symbol S;
}
{
	<tAP> expresion(at) <tCP> 
	{
		sf.add_to_atts(att,at);
		sf.heredar_valores(att,at);
	}
|   <tINT2CHAR> <tAP> expresion(at) <tCP> 
	{
		if(at.type!=Symbol.Types.INT)
		{
			throw new ErrorSemantico("int2char solo acepta enteros");
		}
		sf.asignar_valores(att,at,"int2char(" + at.code + ")",Symbol.Types.CHAR, Symbol.ParameterClass.VAL, st.level);
		sf.heredar_valores(att,at);
	}
|   <tCHAR2INT> <tAP> expresion(at) <tCP> 
	{
		if(at.type!=Symbol.Types.CHAR)
		{
			throw new ErrorSemantico("char2int solo acepta caracteres");
		}
		sf.asignar_valores(att,at,"char2int(" + at.code + ")",Symbol.Types.INT, Symbol.ParameterClass.VAL, st.level);
		sf.heredar_valores(att,at);
	}
|   t=<tID> ((<tAP> (function_call(at)) <tCP> ))?  
	{
		try
		{
			S = st.getSymbol(t.image);
			sf.cambiarTipos(at, S);
			sf.asignar_valores(att,at,t.image,S.type, S.parClass, st.level);
			sf.checkPrimarioIds(at);
			at = new Attributes();			
		}
		catch(SymbolNotFoundException ex)
		{
			throw new ErrorSemantico(t.image + " no existe.");
		}

	}
|   ("-")? t= <tCONST_INT> 
    {
		sf.asignar_valores(att,at,t.image,Symbol.Types.INT, Symbol.ParameterClass.VAL, st.level);
	}
|   t= <tCONST_CHAR> 
	{
		sf.asignar_valores(att,at,t.image,Symbol.Types.CHAR, Symbol.ParameterClass.VAL, st.level);
	}
|   t= <tCONST_STRING> 
	{
		sf.asignar_valores(att,at,t.image,Symbol.Types.STRING, Symbol.ParameterClass.VAL, st.level);
	}
|   t= <tTRUE>	
	{
		sf.asignar_valores(att,at,"true",Symbol.Types.BOOL, Symbol.ParameterClass.VAL, st.level);
	}
|   t= <tFALSE> 
	{
		sf.asignar_valores(att,at,"false",Symbol.Types.BOOL, Symbol.ParameterClass.VAL, st.level);
	}
}


void function_call(Attributes att) :
{ 
	Attributes at = new Attributes();
}
{
 ( expresion(at)
 {
	sf.add_to_atts(att,at);
	sf.heredar_valores(att,at);
	at = new Attributes();
 } 
 ((<tCOMA>) expresion(at)
 {
	sf.add_to_atts(att,at);
	sf.heredar_valores(att,at);
}
)* 
 ) 
 {
	
 }
}


void tipo_variable(Attributes atTypes, Attributes atIsArray):
{ 
	Token i;
}
{
	< tARRAY >
	<tAP>
	("-")? i = < tCONST_INT >
		{
			atTypes.inicio = Integer.parseInt(i.image);
			
			//at.parList.add(new Symbol(i.image,Symbol.Types.INT));
			
		}
	("..")
	("-")? i = < tCONST_INT >
		{
			atTypes.fin = Integer.parseInt(i.image);
			// at.parList.add(new Symbol(i.image,Symbol.Types.INT));
			atTypes.type = Symbol.Types.ARRAY;
		}
	<tCP>
	 <tOF> tipo_variable_simple(atIsArray)
|   tipo_variable_simple(atTypes)
}

void tipo_variable_simple(Attributes at):
{

}
{
		< tBOOL > 
			{
				at.type = Symbol.Types.BOOL;
			}
	|	< tCHAR >
			{
				at.type = Symbol.Types.CHAR;
			}
	|	< tINT >
			{
				at.type = Symbol.Types.INT;
			}
}

